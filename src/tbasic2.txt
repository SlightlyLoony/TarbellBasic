*
*
* TARBELL BASIC SOURCE MODULE #2
* COPYRIGHT (C) 1978 TARBELL ELECTRONICS
* LAST CHANGE TO THIS MODULE MADE 3-15-79.
*
STARS	LXI	SP,STACK+100	;INITIALIZE THE STACK
	MVI	A,0C9H	;SET UP RET FOR IN/OUT INST.
	STA	IOST+2
	CALL	CHCK	;SET THE INTEGRITY FLAG
	STA	CHECK
	LDA	SRFLG	;CHECK FOR PREVIOUS INITIALIZATION
	ANA	A
	JNZ	INTR	;GOTO TO NORMAL INITIALIZATION
	XRA	A	;INITIALIZE EDIT FLAGS
	STA	MERR	;INITIALIZE THE MATH ERROR FLAG
	STA	CFLG	;CLEAR CHANGE COMMAND FLAG.
	LXI	H,0
	SHLD	DMPMM
	STA	EDITM
	CALL	CRLF	;DO CARRIAGE-RETURN, LINE-FEED.
S0000	LXI	H,BEGIN	;DUMP THE GREETING
	CALL	MSGER
	MVI	A,0FFH	;SET SRFLG
	STA	SRFLG	;FALLS THRU TO INTR.
* RTN. E.31
* INITIALIZER
*
INTR	LXI	SP,STACK+100	;SET STACK POINTER.
	LHLD	SSSS	;CHECK END OF MEMORY FLAG.
	MOV	A,H
	ORA	L	;IS IT 0?
	JNZ	INTR1	;NOPE.
	LXI	H,PNTR+1	;GET LAST USED ADDRESS.
INTR2	INX	H
	MVI	M,0	;CHECK ADDRESS'S EXISTENCE.
	MOV	A,M
	ANA	A	;SET FLAGS.
	JZ	INTR2	;IT EXISTS.
	DCX	H	;GET LAST EXISTING ADDRESS.
INTR1	SHLD	MEND	;SET END OF MEMORY POINTER.
	XRA	A	;CLEAR FOLLOWING FLAGS.
	STA	CHKEN
	STA	BFLAG
	STA	CSST
	STA	RUNF
	STA	EDITM
	INR	A	;SET CMND = 1.
	STA	CMND
	LXI	H,0	;CLEAR DUMP MEMORY MODE.
	SHLD	DMPMM
	LXI	H,PNTR+2	;GET FIRST ADR FOR SOURCE CODE.
	SHLD	EBSC	;SET END OF BASIC ADDRESS.
	CALL	PNEW1	;INITIALIZE SOURCE.
	MVI	A,0C3H	;STORE JUMP INSTRUCTION.
	STA	START
	LXI	H,RSTRT
	SHLD	START+1
	SHLD	101H	;GO AROUND I/O INIT NOW.
	JMP	RSTRT	;WE'RE OFF AND RUNNING.
BEGIN	DB	0DH,'TARBELL BASIC',0DH
	DB	'BY REAL TIME MICROSYSTEMS, CHULA VISTA, CA'
	DB	0DH
	DB	'FOR TARBELL ELECTRONICS, CARSON, CA',0DH
	DB	'RELEASE 11.8  MAR 15, 1979',8DH
*
* NOTE: FIRST DIGIT OF RELEASE NUMBER IS RTM'S.
*	SECOND DIGIT IS TARBELL ELECTRONICS'.
*	NEW COMPANIES SHOULD ADD . AND NUMBER.
*
LINK02	LINK	A:TBASIC3
